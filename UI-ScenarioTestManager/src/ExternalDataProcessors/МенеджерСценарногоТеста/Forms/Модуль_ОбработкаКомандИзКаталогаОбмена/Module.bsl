
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Отказ = Истина; // форма не предназначена для открытия
КонецПроцедуры

#Область ОбменСообщениями_СозданиеКоманды

Функция СоздатьИсходящееСообщениеXML(Знач СвойстваСообщения) Экспорт
	
	XMLСтрока = "";  	
	
	Попытка
		
		// Создать объект записи XML и открыть файл
		НоваяЗаписьXML = Новый ЗаписьXML;
		НоваяЗаписьXML.УстановитьСтроку("UTF-8");
		
		НоваяЗаписьXML.ЗаписатьОбъявлениеXML();
		
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("answer");
		
		НоваяЗаписьXML.ЗаписатьАтрибут("Версия","1.0");
		
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("parameters");
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		
		// формируем сообщение
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("message");
		НоваяЗаписьXML.ЗаписатьАтрибут("type","command");
		НоваяЗаписьXML.ЗаписатьАтрибут("name",СвойстваСообщения.ИмяКоманды);
		Если  СвойстваСообщения.Свойство("Результат") Тогда
			НоваяЗаписьXML.ЗаписатьАтрибут("result",СвойстваСообщения.Результат);
		Иначе
			НоваяЗаписьXML.ЗаписатьАтрибут("result","");
		КонецЕсли;
		НоваяЗаписьXML.ЗаписатьАтрибут("id",СтрЗаменить(Строка(СвойстваСообщения.НомерСообщения),Символы.НПП,""));
		
		Если СвойстваСообщения.Свойство("ТекстОшибки") Тогда
			НоваяЗаписьXML.ЗаписатьНачалоЭлемента("error");
			НоваяЗаписьXML.ЗаписатьСекциюCDATA(СвойстваСообщения.ТекстОшибки);
			НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;		
		
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		
		// Конец основного тега
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();         
		XMLСтрока = НоваяЗаписьXML.Закрыть();
		
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат XMLСтрока;
	
КонецФункции

#КонецОбласти


#Область ОбменСообщениями_Чтение

Функция РазобратьВходящееСообщениеXML(Знач XMLСтрока) Экспорт
	
	СвойстваСообщения = новый Структура("ТекстОшибки","");
	
	РезультатРазбора = Истина;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ИгнорироватьПробелы = Ложь;
	
	Попытка
		
		ЧтениеXML.УстановитьСтроку(XMLСтрока);
		
		Пока ЧтениеXML.Прочитать() Цикл 			
			
			Если ЧтениеXML.Имя = "request" Тогда // это запрос
				РазобратьЗапрос(ЧтениеXML,СвойстваСообщения);
			ИначеЕсли ЧтениеXML.Имя = "answer" Тогда // это ответ
				РазобратьОтвет(ЧтениеXML,СвойстваСообщения);
			КонецЕсли;
			
		КонецЦикла;
		
		ЧтениеXML.Закрыть();
		
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		СвойстваСообщения.ТекстОшибки = ТекстОшибки;
		Сообщить(ТекстОшибки);
	КонецПопытки; 
	
	Возврат СвойстваСообщения;
	
КонецФункции

Процедура РазобратьЗапрос(ЧтениеXML,СвойстваСообщения)
	
	Пока ЧтениеXML.Прочитать() Цикл 			
		
		Если ЧтениеXML.Имя = "message" Тогда 
			СвойстваСообщения.Вставить("ТипСообщения",ЧтениеXML.ПолучитьАтрибут("type"));
			СвойстваСообщения.Вставить("Наименование",ЧтениеXML.ПолучитьАтрибут("name"));
			СвойстваСообщения.Вставить("Результат",ЧтениеXML.ПолучитьАтрибут("result"));
			СвойстваСообщения.Вставить("НомерСообщения",Число(ЧтениеXML.ПолучитьАтрибут("id")));
			ПрочитатьВходныеЗапроса(ЧтениеXML,СвойстваСообщения);
		ИначеЕсли ЧтениеXML.Имя = "parameters" Тогда 
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочитатьВходныеЗапроса(ЧтениеXML,СвойстваСообщения)
	
	Пока ЧтениеXML.Прочитать() Цикл 			
		
		Если ЧтениеXML.Имя = "items" И ЧтениеXML.ТипУзла=ТипУзлаXML.НачалоЭлемента Тогда 
			СвойстваСообщения.Вставить("items",новый Структура);
		ИначеЕсли ЧтениеXML.Имя = "item" И ЧтениеXML.ТипУзла=ТипУзлаXML.НачалоЭлемента Тогда 
			Если СвойстваСообщения.Свойство("items") Тогда
				ИмяАтрибута = ЧтениеXML.ПолучитьАтрибут("name");
				СвойстваСообщения.items.Вставить(ИмяАтрибута);
				ЧтениеXML.Прочитать();
				СвойстваСообщения.items[ИмяАтрибута] = ЧтениеXML.Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура РазобратьОтвет(ЧтениеXML,СвойстваСообщения)
	
	Пока ЧтениеXML.Прочитать() Цикл 			
		
		Если ЧтениеXML.Имя = "message" Тогда 
			СвойстваСообщения.Вставить("ТипСообщения",ЧтениеXML.ПолучитьАтрибут("type"));
			СвойстваСообщения.Вставить("Наименование",ЧтениеXML.ПолучитьАтрибут("name"));
			СвойстваСообщения.Вставить("Результат",ЧтениеXML.ПолучитьАтрибут("result"));
			СвойстваСообщения.Вставить("НомерСообщения",Число(ЧтениеXML.ПолучитьАтрибут("id")));
		ИначеЕсли ЧтениеXML.Имя = "parameters" Тогда
		ИначеЕсли ЧтениеXML.Имя = "error" И ЧтениеXML.ТипУзла=ТипУзлаXML.НачалоЭлемента Тогда
			ЧтениеXML.Прочитать();
			СвойстваСообщения.Вставить("error",ЧтениеXML.Значение);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
