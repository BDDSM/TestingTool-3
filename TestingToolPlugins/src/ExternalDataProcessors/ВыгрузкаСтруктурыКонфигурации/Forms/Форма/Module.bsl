
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяКонфигурации =  Метаданные.Имя;
	СинонимКонфигурации = Метаданные.Синоним;
	ВерсияКонфигурации = Метаданные.Версия;
	
КонецПроцедуры


&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	Диалог.Заголовок = "Выберите файл"; 
	Диалог.ПолноеИмяФайла = ""; 
	Фильтр = "XML-файл (*.xml)|*.xml"; 
	Диалог.Фильтр = Фильтр; 
	Диалог.МножественныйВыбор = Ложь; 
	ВыборФайлаСохранения = новый ОписаниеОповещения("ВыборФайлаСохраненияСтруктурыКонфигурации",ЭтотОбъект);
	Диалог.Показать(ВыборФайлаСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаСохраненияСтруктурыКонфигурации(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	 Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКФайлу = ВыбранныеФайлы[0]; 
	КонецЕсли; 
	
КонецПроцедуры

#Область СохранениеСтруктурыКонфигурации

&НаСервере
Процедура ВыгрузитьРеквизиты(НоваяЗаписьXML,Элемент,ВыгружатьСтандартныеРеквизиты=Истина)
	
	Если ВыгружатьСтандартныеРеквизиты=Истина Тогда
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("СтандартныеРеквизиты");
		НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Элемент.СтандартныеРеквизиты.Количество()));
		Для каждого Реквизит из Элемент.СтандартныеРеквизиты Цикл
			НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Реквизит");
			НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Реквизит.Имя);	
			НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Реквизит.Синоним);	
			НоваяЗаписьXML.ЗаписатьАтрибут("Комментарий",Реквизит.Комментарий);	
			НоваяЗаписьXML.ЗаписатьАтрибут("ТипРеквизита","СтандартныйРеквизит");	
			НоваяЗаписьXML.ЗаписатьКонецЭлемента();		
		КонецЦикла;
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();			
	КонецЕсли;
	
	НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Реквизиты");
	НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Элемент.Реквизиты.Количество()));
	Для каждого Реквизит из Элемент.Реквизиты Цикл
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Реквизит");
		НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Реквизит.Имя);	
		НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Реквизит.Синоним);	
		НоваяЗаписьXML.ЗаписатьАтрибут("Комментарий",Реквизит.Комментарий);	
		НоваяЗаписьXML.ЗаписатьАтрибут("ТипРеквизита","Реквизит");	
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();		
	КонецЦикла;
	НоваяЗаписьXML.ЗаписатьКонецЭлемента();						
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьТабличныеЧасти(НоваяЗаписьXML,Элемент)
	
	НоваяЗаписьXML.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
	НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Элемент.ТабличныеЧасти.Количество()));
	Для каждого ТЧ из Элемент.ТабличныеЧасти Цикл
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("ТЧ");
		НоваяЗаписьXML.ЗаписатьАтрибут("Имя",ТЧ.Имя);	
		НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",ТЧ.Синоним);	
		НоваяЗаписьXML.ЗаписатьАтрибут("Комментарий",ТЧ.Комментарий);	
		
		ВыгрузитьРеквизиты(НоваяЗаписьXML,ТЧ);
		
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	НоваяЗаписьXML.ЗаписатьКонецЭлемента();						
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьФормы(НоваяЗаписьXML,Элемент)
	
	НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Формы");
	НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Элемент.Формы.Количество()));
	Для каждого Форма из Элемент.Формы Цикл
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Форма");
		НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Форма.Имя);	
		НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Форма.Синоним);	
		НоваяЗаписьXML.ЗаписатьАтрибут("ТипФормы",Строка(Форма.ТипФормы));	
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();		
	КонецЦикла;
	НоваяЗаписьXML.ЗаписатьКонецЭлемента();	
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьСтруктуруКонфигурацииНаСервере()
	
	
	XMLСтрока = "";
	
	
	Попытка
		
		// Создать объект записи XML и открыть файл
		НоваяЗаписьXML = Новый ЗаписьXML;
		НоваяЗаписьXML.УстановитьСтроку("UTF-8");
		
		НоваяЗаписьXML.ЗаписатьОбъявлениеXML();
		
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Configuration");
		
		НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Description");
		НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Метаданные.Имя);
		НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Метаданные.Синоним);
		НоваяЗаписьXML.ЗаписатьАтрибут("Версия",Метаданные.Версия);
		НоваяЗаписьXML.ЗаписатьАтрибут("АвторскиеПрава",Метаданные.АвторскиеПрава);
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		
		Если Справочники=Истина Тогда
			НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Справочники");
			НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Метаданные.Справочники.Количество()));
			Для каждого Элемент из Метаданные.Справочники Цикл
				НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Справочник");
				НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Элемент.Имя);	
				НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Элемент.Синоним);	
				НоваяЗаписьXML.ЗаписатьАтрибут("Комментарий",Элемент.Комментарий);	
				
				Если ВыгружатьРеквизиты Тогда
					ВыгрузитьРеквизиты(НоваяЗаписьXML,Элемент);					
				КонецЕсли;
				
				Если ВыгружатьТабличныеЧасти Тогда
					ВыгрузитьТабличныеЧасти(НоваяЗаписьXML,Элемент);
				КонецЕсли;
				
				Если ВыгружатьФормы = Истина Тогда
					ВыгрузитьФормы(НоваяЗаписьXML,Элемент);
				КонецЕсли;
				
				НоваяЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;
			НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
		
		Если Документы=Истина Тогда
			НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Документы");
			НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Метаданные.Документы.Количество()));
			Для каждого Элемент из Метаданные.Документы Цикл
				НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
				НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Элемент.Имя);	
				НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Элемент.Синоним);	
				НоваяЗаписьXML.ЗаписатьАтрибут("Комментарий",Элемент.Комментарий);	
				
				Если ВыгружатьРеквизиты Тогда
					ВыгрузитьРеквизиты(НоваяЗаписьXML,Элемент);					
				КонецЕсли;
				
				Если ВыгружатьТабличныеЧасти Тогда
					ВыгрузитьТабличныеЧасти(НоваяЗаписьXML,Элемент);
				КонецЕсли;
				
				Если ВыгружатьФормы = Истина Тогда
					ВыгрузитьФормы(НоваяЗаписьXML,Элемент);
				КонецЕсли;
				
				НоваяЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;
			НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
		
		Если Обработки=Истина Тогда
			НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Обработки");
			НоваяЗаписьXML.ЗаписатьАтрибут("Количество",Строка(Метаданные.Обработки.Количество()));
			Для каждого Элемент из Метаданные.Обработки Цикл
				НоваяЗаписьXML.ЗаписатьНачалоЭлемента("Обработка");
				НоваяЗаписьXML.ЗаписатьАтрибут("Имя",Элемент.Имя);	
				НоваяЗаписьXML.ЗаписатьАтрибут("Синоним",Элемент.Синоним);	
				НоваяЗаписьXML.ЗаписатьАтрибут("Комментарий",Элемент.Комментарий);	
				
				Если ВыгружатьРеквизиты Тогда
					ВыгрузитьРеквизиты(НоваяЗаписьXML,Элемент,Ложь);					
				КонецЕсли;
				
				Если ВыгружатьТабличныеЧасти Тогда
					ВыгрузитьТабличныеЧасти(НоваяЗаписьXML,Элемент);
				КонецЕсли;
				
				Если ВыгружатьФормы = Истина Тогда
					ВыгрузитьФормы(НоваяЗаписьXML,Элемент);
				КонецЕсли;
				
				НоваяЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;
			НоваяЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
		
		
		// Конец основного тега
		НоваяЗаписьXML.ЗаписатьКонецЭлемента();         
		XMLСтрока = НоваяЗаписьXML.Закрыть();
		
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат XMLСтрока;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьСтруктуруКонфигурации(Команда)
	
	XMLСтрока = ВыгрузитьСтруктуруКонфигурацииНаСервере();
	
	Если ЗначениеЗаполнено(XMLСтрока) Тогда
		
		Документ = новый ТекстовыйДокумент;
		Документ.УстановитьТекст(XMLСтрока);
		СохранениеФайлаСтруктурыКонфигурации = новый ОписаниеОповещения("СохранениеФайлаСтруктурыКонфигурации",ЭтотОбъект);
		Попытка
			Документ.НачатьЗапись(СохранениеФайлаСтруктурыКонфигурации,ПутьКФайлу,"UTF-8");
		Исключение
			Документ.Записать(ПутьКФайлу,"UTF-8");
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранениеФайлаСтруктурыКонфигурации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат=Истина Тогда
		Сообщить("Файл конфигурации записан успешно!");
	Иначе
		Сообщить("При сохранении файла конфигурации произошла ошибка!");	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти