
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактироватьКонструктором = Истина;
	ИдентификаторКонструктора = "TaskTestBuilder";
	МенеджерТестированияКонфигурацияТестирование = Истина;
	МенеджерТестирования = Справочники.ТестируемыеКлиенты.ТекущаяБаза1С;
	ПортТестирования = "1538"; 
	
	// Открыта форма редактирования
	Если ЗначениеЗаполнено(Параметры.Задание) Тогда		
		Задание = Параметры.Задание;
	КонецЕсли;
	
	Если Параметры.Свойство("ОбъектыНазначения") Тогда
		Если ТипЗнч(Параметры.ОбъектыНазначения) = Тип("Массив")
			И Параметры.ОбъектыНазначения.Количество()>0 Тогда			
			Задание = Параметры.ОбъектыНазначения[0];
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Задание) Тогда
		Элементы.СоздатьНовоеЗадание.Заголовок = "Применить изменения";
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Задания.Наименование,
		|	Задания.ID,
		|	Задания.Автор,
		|	Задания.ГруппаЗадания,
		|	Задания.Ответственный,
		|	Задания.ИдентификаторКонструктора,
		|	Задания.Родитель
		|ИЗ
		|	Справочник.Задания КАК Задания
		|ГДЕ
		|	Задания.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",Задание);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			
			ВызватьИсключение "Ошибка редактирования запроса в конструкторе...";
			
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		                    		
		Наименование = Выборка.Наименование;
		TaskID = Выборка.ID; 
		Ответственный = Выборка.Ответственный;
		ГруппаЗадания = Выборка.ГруппаЗадания;
		Родитель =  Выборка.Родитель;
		
		// получим тест из регистра
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПеременныеЗаданий.ЗначениеПеременной КАК Значение
		|ИЗ
		|	РегистрСведений.ПеременныеЗаданий КАК ПеременныеЗаданий
		|ГДЕ
		|	ПеременныеЗаданий.Задание = &Задание
		|	И ПеременныеЗаданий.ИмяПеременной = &ИмяПеременной
		|	И ПеременныеЗаданий.ЭтоПараметрНастройки = ИСТИНА";
		Запрос.УстановитьПараметр("Задание",Задание);
		Запрос.УстановитьПараметр("ИмяПеременной",Справочники.ИменаПеременных.Тест);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Тест = Выборка.Значение;
			ЭтоСценарныйТест = (Тест.ТипТеста=Перечисления.ТипыТестов.СценарныйТест);
			Элементы.ГруппаЕслиСценарныйТест.Видимость = ЭтоСценарныйТест;
		КонецЕсли;
		
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПеременныеЗаданий.ЗначениеПеременной КАК Значение
		|ИЗ
		|	РегистрСведений.ПеременныеЗаданий КАК ПеременныеЗаданий
		|ГДЕ
		|	ПеременныеЗаданий.Задание = &Задание
		|	И ПеременныеЗаданий.ИмяПеременной = &ИмяПеременной
		|	И ПеременныеЗаданий.ЭтоПараметрНастройки = ЛОЖЬ";
		Запрос.УстановитьПараметр("Задание",Задание);
		Запрос.УстановитьПараметр("ИмяПеременной","%ПортТестирования%");
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ПортТестирования = Выборка.Значение;
		КонецЕсли;
		
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПеременныеЗаданий.ЗначениеПеременной КАК Значение
		|ИЗ
		|	РегистрСведений.ПеременныеЗаданий КАК ПеременныеЗаданий
		|ГДЕ
		|	ПеременныеЗаданий.Задание = &Задание
		|	И ПеременныеЗаданий.ИмяПеременной = &ИмяПеременной
		|	И ПеременныеЗаданий.ЭтоПараметрНастройки = ИСТИНА";
		Запрос.УстановитьПараметр("Задание",Задание);
		Запрос.УстановитьПараметр("ИмяПеременной","НеИспользоватьСсылкуНаТест");
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			НеИспользоватьСсылкуНаТест = Выборка.Значение;
		КонецЕсли;
		
		Элементы.ГруппаСвойстваТеста.Видимость = НЕ НеИспользоватьСсылкуНаТест;		
		
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПеременныеЗаданий.ЗначениеПеременной КАК Значение
		|ИЗ
		|	РегистрСведений.ПеременныеЗаданий КАК ПеременныеЗаданий
		|ГДЕ
		|	ПеременныеЗаданий.Задание = &Задание
		|	И ПеременныеЗаданий.ИмяПеременной = &ИмяПеременной
		|	И ПеременныеЗаданий.ЭтоПараметрНастройки = ИСТИНА";
		Запрос.УстановитьПараметр("Задание",Задание);
		Запрос.УстановитьПараметр("ИмяПеременной",Справочники.ИменаПеременных.МенеджерТестирования);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			МенеджерТестирования = Выборка.Значение;
		КонецЕсли;
		
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПеременныеЗаданий.ЗначениеПеременной КАК Значение
		|ИЗ
		|	РегистрСведений.ПеременныеЗаданий КАК ПеременныеЗаданий
		|ГДЕ
		|	ПеременныеЗаданий.Задание = &Задание
		|	И ПеременныеЗаданий.ИмяПеременной = &ИмяПеременной
		|	И ПеременныеЗаданий.ЭтоПараметрНастройки = ИСТИНА";
		Запрос.УстановитьПараметр("Задание",Задание);
		Запрос.УстановитьПараметр("ИмяПеременной",Справочники.ИменаПеременных.ТестируемыйКлиент);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ТестируемыйКлиент = Выборка.Значение;
		КонецЕсли;
		
		
	Иначе
		
		//Элементы.Задание.Видимость = Ложь;
		
	КонецЕсли;
	
	// видимость 
	Элементы.МенеджерТестирования.Видимость = НЕ МенеджерТестированияКонфигурацияТестирование;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.СтраницыОбработки.ОтображениеСтраниц=ОтображениеСтраницФормы.Нет;
	ОтработатьПеремещениеПоСтраницам();
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	Если Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИнициализация Тогда
		Если НЕ ПроверитьНаличиеПодобноегоЗадания(Наименование,TaskID,Задание) Тогда
			Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаНастройкиВыполненияТеста;
		КонецЕсли;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаНастройкиВыполненияТеста Тогда
		Если НЕ ЗначениеЗаполнено(Тест) И НеИспользоватьСсылкуНаТест=Ложь Тогда
			Сообщить("Выберите тест прежде!");
		Иначе
			Если НЕ ЗначениеЗаполнено(ТестируемыйКлиент) Тогда
				Сообщить("Если не указать тестируемый клиент для этого задания, тогда не возможно его будет запустить самостоятельно!");
			КонецЕсли;
			// получим данные по существующему заданию
			Если ЗначениеЗаполнено(Задание) Тогда
				ПолучитьСоставПоЗаданию();
			КонецЕсли; 			
			Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаСоставЗадания;
		КонецЕсли;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаСоставЗадания Тогда
		Если ПараметрыУказанияПараметровЗаданияКорректны() Тогда
			ОбобщениеHTML = СформироватьОписаниеСоздаваемогоЗдания();
			Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИтого;
		КонецЕсли;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИтого Тогда
	КонецЕсли;
	
	ОтработатьПеремещениеПоСтраницам();
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	Если Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИнициализация Тогда
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаНастройкиВыполненияТеста Тогда
		Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИнициализация;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаСоставЗадания Тогда
		Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаНастройкиВыполненияТеста;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИтого Тогда
		Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаСоставЗадания;
	КонецЕсли;	
	
	ОтработатьПеремещениеПоСтраницам();
КонецПроцедуры

&НаСервере
Функция СоздатьНовоеЗаданиеНаСервере()
	
	Отказ = Ложь;	
	ЗаданиеОбъект = Неопределено;
	
	НачатьТранзакцию();
	
	// Создаем/ обновляем задание
	Если ЗначениеЗаполнено(Задание) Тогда
		ЗаданиеОбъект = Задание.ПолучитьОбъект();
	Иначе
		ЗаданиеОбъект = Справочники.Задания.СоздатьЭлемент();
	КонецЕсли;	
	
	ЗаданиеОбъект.Наименование = Наименование;
	ЗаданиеОбъект.ID = TaskID;
	ЗаданиеОбъект.Родитель = Родитель;
	ЗаданиеОбъект.ГруппаЗадания = ГруппаЗадания;
	ЗаданиеОбъект.РедактироватьКонструктором = РедактироватьКонструктором;
	ЗаданиеОбъект.ИдентификаторКонструктора = ИдентификаторКонструктора;
	ЗаданиеОбъект.Ответственный = Ответственный;
	Если НЕ ЗначениеЗаполнено(ЗаданиеОбъект.Автор) Тогда
		ЗаданиеОбъект.Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Попытка
		ЗаданиеОбъект.Записать();
		Задание = ЗаданиеОбъект.Ссылка;
	Исключение
		ОтменитьТранзакцию();
		Отказ = Истина;
		Сообщить(ОписаниеОшибки());		
		Возврат НЕ Отказ;
	КонецПопытки;
	
	
	// обновляем регистр состав
	НаборЗаписей = РегистрыСведений.СоставЗаданий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Задание.Установить(Задание);
	
	ПорядокВыполнения = 1;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ПорядокВыполнения = ПорядокВыполнения;
	НоваяЗапись.Действие = ДействиеВыполнитьТест;
	ПорядокВыполнения = ПорядокВыполнения +1;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ПорядокВыполнения = ПорядокВыполнения;
	НоваяЗапись.Действие = ДействиеОжиданияВыполненияТеста;
	ПорядокВыполнения = ПорядокВыполнения +1;
	
	Если ЗначениеЗаполнено(ДействиеЗакрытьПриложение) Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ПорядокВыполнения = ПорядокВыполнения;
		НоваяЗапись.Действие = ДействиеЗакрытьПриложение;
		ПорядокВыполнения = ПорядокВыполнения +1;  		
	КонецЕсли;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ПорядокВыполнения = ПорядокВыполнения;
	НоваяЗапись.Действие = ДействиеЗагрузкиЛога;
	ПорядокВыполнения = ПорядокВыполнения +1;
	
	Если ЗначениеЗаполнено(ДействиеЗакрытьПриложение) Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ПорядокВыполнения = ПорядокВыполнения;
		НоваяЗапись.Действие = ДействиеЗакрытьПриложение;
		ПорядокВыполнения = ПорядокВыполнения +1;  		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	// создаем необходимые переменные
	НаборЗаписей = РегистрыСведений.ПеременныеЗаданий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Задание.Установить(Задание);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ИмяПеременной = Справочники.ИменаПеременных.Тест;
	НоваяЗапись.ЗначениеПеременной = Тест;
	НоваяЗапись.ЭтоПараметрНастройки = Истина;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ИмяПеременной = "%Тест%";
	НоваяЗапись.ЗначениеПеременной = Тест;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ИмяПеременной = "%ПутьКФайлуТеста%";
	НоваяЗапись.ЗначениеПеременной = Тест;
	НоваяЗапись.ИспользоватьФункцию = Истина;
	НоваяЗапись.ИмяФункции = "ПутьКФайлуТеста";
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ИмяПеременной = "%ПортТестирования%";
	НоваяЗапись.ЗначениеПеременной = ПортТестирования;
	
	Если ЭтоСценарныйТест=Истина Тогда
		Если ЗначениеЗаполнено(МенеджерТестирования) Тогда
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Задание = Задание;
			НоваяЗапись.ИмяПеременной = Справочники.ИменаПеременных.МенеджерТестирования;
			НоваяЗапись.ЗначениеПеременной = МенеджерТестирования;
			НоваяЗапись.ЭтоПараметрНастройки = Истина;
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Задание = Задание;
			НоваяЗапись.ИмяПеременной = "%СтрокаСоединенияМенеджер%";
			НоваяЗапись.ЗначениеПеременной = МенеджерТестирования;
			НоваяЗапись.ИспользоватьФункцию = Истина;
			НоваяЗапись.ИмяФункции = "СтрокаСоединения";
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТестируемыйКлиент) Тогда
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ИмяПеременной = Справочники.ИменаПеременных.ТестируемыйКлиент;
		НоваяЗапись.ЗначениеПеременной = ТестируемыйКлиент;
		НоваяЗапись.ЭтоПараметрНастройки = Истина;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ИмяПеременной = "%СтрокаСоединенияИБ%";
		НоваяЗапись.ЗначениеПеременной = ТестируемыйКлиент;
		НоваяЗапись.ИспользоватьФункцию = Истина;
		НоваяЗапись.ИмяФункции = "СтрокаСоединенияИБ";
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ИмяПеременной = "%ИмяПользователя1С%";
		НоваяЗапись.ЗначениеПеременной = ТестируемыйКлиент;
		НоваяЗапись.ИспользоватьФункцию = Истина;
		НоваяЗапись.ИмяФункции = "ИмяПользователя1С";
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ИмяПеременной = "%ПарольПользователя1С%";
		НоваяЗапись.ЗначениеПеременной = ТестируемыйКлиент;
		НоваяЗапись.ИспользоватьФункцию = Истина;
		НоваяЗапись.ИмяФункции = "ПарольПользователя1С";		
	КонецЕсли;
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Задание = Задание;
	НоваяЗапись.ИмяПеременной = "%НомерПроверки%";
	НоваяЗапись.ЗначениеПеременной = 0;

	// настройка задания
	Если НеИспользоватьСсылкуНаТест Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задание = Задание;
		НоваяЗапись.ИмяПеременной = "НеИспользоватьСсылкуНаТест";
		НоваяЗапись.ЗначениеПеременной = НеИспользоватьСсылкуНаТест;
		НоваяЗапись.ЭтоПараметрНастройки = Истина;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	
	ЗафиксироватьТранзакцию();
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаКлиенте
Процедура СоздатьНовоеЗадание(Команда)
	Если СоздатьНовоеЗаданиеНаСервере()=Истина Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтработатьПеремещениеПоСтраницам()
	
	Если Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИнициализация Тогда
		Элементы.Назад.Видимость = Ложь;
		Элементы.Вперед.Видимость = Истина;
		Элементы.СоздатьНовоеЗадание.Видимость = Ложь;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаНастройкиВыполненияТеста Тогда
		Элементы.Назад.Видимость = Истина;
		Элементы.Вперед.Видимость = Истина;
		Элементы.СоздатьНовоеЗадание.Видимость = Ложь;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаСоставЗадания Тогда
		Элементы.Назад.Видимость = Истина;
		Элементы.Вперед.Видимость = Истина;
		Элементы.СоздатьНовоеЗадание.Видимость = Ложь;
	ИначеЕсли Элементы.СтраницыОбработки.ТекущаяСтраница=Элементы.СтраницаИтого Тогда
		Элементы.Назад.Видимость = Истина;
		Элементы.Вперед.Видимость = Ложь;
		Элементы.СоздатьНовоеЗадание.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(TaskID) Тогда
		TaskID = СценарноеТестированиеКлиентСервер.СформироватьАвтоматическиИдентификаторТеста(Наименование);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьНаличиеПодобноегоЗадания(Знач Наименование,Знач TaskID,Знач Задание)   	
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		Сообщить("Укажите наименование нового задания!");
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(TaskID) Тогда
		Сообщить("Укажите идентификатор нового задания!");
		Возврат Истина;
	КонецЕсли;
	
	// проверим, есть ли такое задание
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Задания.Ссылка,
	|	Задания.ID,
	|	Задания.Наименование
	|ИЗ
	|	Справочник.Задания КАК Задания
	|ГДЕ
	|	(Задания.Наименование = &Наименование
	|			ИЛИ Задания.ID = &TaskID)
	|	И НЕ Задания.Ссылка = &Задание";
	
	Запрос.УстановитьПараметр("Наименование",Наименование);
	Запрос.УстановитьПараметр("TaskID",TaskID);
	Запрос.УстановитьПараметр("Задание",Задание);

	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Сообщить("Уже существет задание с подобным именем или идентификатором. Измените вводимые данные! Наименование - " +Выборка.Наименование+" Идентификатор - "+Выборка.TaskID);
		
	КонецЦикла;
	
	
	Возврат Истина;
	
	
КонецФункции

&НаКлиенте
Функция ПараметрыУказанияПараметровЗаданияКорректны()
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(ДействиеВыполнитьТест) Тогда
		Сообщить("Укажите действие 'Выполнить тест'");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДействиеОжиданияВыполненияТеста) Тогда
		Сообщить("Укажите действие 'Ожидание выполнения теста'");
		Отказ = Истина;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ДействиеЗагрузкиЛога) Тогда
		Сообщить("Укажите действие 'Загрузка лога'");
		Отказ = Истина;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаСервере
Процедура ПолучитьСоставПоЗаданию()
	
	Запрос = новый Запрос;
	ЗАпрос.Текст = "ВЫБРАТЬ
	|	СоставЗаданий.Действие  как Ссылка
	|ИЗ
	|	РегистрСведений.СоставЗаданий КАК СоставЗаданий
	|ГДЕ
	|	СоставЗаданий.Задание = &Задание
	|
	|УПОРЯДОЧИТЬ ПО
	|	СоставЗаданий.ПорядокВыполнения";
	Запрос.УстановитьПараметр("Задание",Задание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	

	Если Выборка.Следующий() Тогда
		ДействиеВыполнитьТест = Выборка.Ссылка;
	КонецЕсли;	
	
	Если Выборка.Следующий() Тогда
		ДействиеОжиданияВыполненияТеста = Выборка.Ссылка;
	КонецЕсли;
	
	Если Выборка.Количество()>3 И Выборка.Следующий() Тогда
		ДействиеЗакрытьПриложение = Выборка.Ссылка;
	КонецЕсли;	
	
	Если Выборка.Следующий() Тогда
		ДействиеЗагрузкиЛога = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьОписаниеСоздаваемогоЗдания()
	
	Html = "<html><head></head><body>";
	Html = Html + "<h3>Свойства задания</h3>";
	Html = Html + "<b>Наименование:</b>  <span color='blue'>"+Наименование+"</span></br>";
	Html = Html + "<b>Идентификатор задания:</b> <span color='blue'>"+TaskID+"</span></br>";
	Html = Html + "<b>Тест:</b> <span color='blue'>"+Тест+"</span></br>";
	Html = Html + "Ответственный: "+?(ЗначениеЗаполнено(Ответственный),Ответственный,"---")+"</br>";
	Html = Html + "Родитель: "+?(ЗначениеЗаполнено(Родитель),Родитель,"---")+"</br>";
	Html = Html + "ГруппаЗадания: "+?(ЗначениеЗаполнено(ГруппаЗадания),ГруппаЗадания,"---")+"</br>";
	Html = Html + "<h3>Структура действий</h3>";
	Html = Html + "<b>Действие запуска выполнения теста:</b> <span color='blue'>"+ДействиеВыполнитьТест+"</span></br>";
	Html = Html + "<b>Действие ожидания выполнения теста:</b> <span color='blue'>"+ДействиеОжиданияВыполненияТеста+"</span></br>";
	Html = Html + "<b>Действие загрузки лога:</b> <span color='blue'>"+ДействиеЗагрузкиЛога+"</b></br>";
	Html = Html + "Действие закрытия приложения: "+ДействиеЗакрытьПриложение+"</br>";
	Html = Html+"</body></html>";
	
	Возврат Html;
	
КонецФункции

&НаКлиенте
Процедура МенеджерТестированияКонфигурацияТестированиеПриИзменении(Элемент)
	Элементы.МенеджерТестирования.Видимость = НЕ МенеджерТестированияКонфигурацияТестирование;
КонецПроцедуры

&НаСервере
Процедура ТестПриИзмененииСервер()
	
	Если ЗначениеЗаполнено(Тест) Тогда
		ЭтоСценарныйТест = (Тест.ТипТеста=Перечисления.ТипыТестов.СценарныйТест);
		Элементы.ГруппаЕслиСценарныйТест.Видимость = ЭтоСценарныйТест;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестПриИзменении(Элемент)
	ТестПриИзмененииСервер();	
КонецПроцедуры

&НаКлиенте
Процедура НеИспользоватьСсылкуНаТестПриИзменении(Элемент)
	Элементы.ГруппаСвойстваТеста.Видимость = НЕ НеИспользоватьСсылкуНаТест;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекстШаблонаКомандыВыполненияСценарногоТеста()
	Возврат " ""%ПутьКИсполняемомуФайлу1С%"" %СтрокаСоединенияМенеджер% /DisableStartupMessages 
	| /Execute ""%ПутьКаталогGIT%\МенеджерСценарногоТеста.epf"" /LogUI /TESTMANAGER 
	| /C""
	| TestUI %ПутьКаталогGIT%\%ПутьКФайлуТеста% 
	| TestLogUI %ПутьКаталогаОтчетаВыполнения% 
	| TestLibDirUI %ПутьКаталогаБлокГотовыхШагов% 
	| TestConnectionStringUI %СтрокаСоединенияИБ% 
	| TestUser1CUI %ИмяПользователя1С% 
	| TestPass1CUI %ПарольПользователя1С% 
	| TestPortUI %ПортТестирования%
	| TestProg1C %ПутьКИсполняемомуФайлу1С% 
	| TestReportNameUI report-%НомерПроверки%_%Тест% 
	| TestReportFormatAllureXML
	|"" ";
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТекстШаблонаКомандыВыполненияЮнитТеста()
	Возврат " ""%ПутьКИсполняемомуФайлу1С%""  %СтрокаСоединения%  
	| /Execute ""%ПутьКаталогGIT%\xddTestRunner.epf""  
	| /C""
	| xddRun ЗагрузчикФайла """"%ПутьКаталогGIT%\%ПутьКФайлуТеста%"""";
	| xddReport ГенераторОтчетаJUnitXML """"%ПутьКаталогаОтчетаВыполнения%\report-%НомерПроверки%_%Тест%.xml""""; \
	| xddShutdown;
	|"" ";
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТекстШаблонаКомандыВыполненияЮнитТестаВсеИзКаталога()
	Возврат " ""%ПутьКИсполняемомуФайлу1С%""  %СтрокаСоединения%  
	| /Execute ""%ПутьКаталогGIT%\xddTestRunner.epf""  
	| /C""
	| xddRun ЗагрузчикКаталога """"%ПутьКаталогGIT%\%КаталогНабораТестов%\"""";
	| xddReport ГенераторОтчетаJUnitXML """"%ПутьКаталогаОтчетаВыполнения%\report_catalog_UnitTests.xml""""; 
	| xddShutdown;
	|"" ";
КонецФункции

#Область ДействиеЗадания

&НаСервереБезКонтекста
Функция СоздатьДействиеЗадания(Знач ИдентификаторКонструктора)
	
	ДействиеСсылка = Справочники.ДействияЗаданий.ПустаяСсылка();
	
	ДействиеОбъект = Справочники.ДействияЗаданий.СоздатьЭлемент();
	
	Попытка
		ДействиеОбъект.Записать();
		ДействиеСсылка = ДействиеОбъект.Ссылка;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Сообщить(ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("СозатьДействиеЗадания",УровеньЖурналаРегистрации.Ошибка,Неопределено,Неопределено,ТекстОшибки);
	КонецПопытки;
	
	Возврат ДействиеСсылка;
КонецФункции

&НаСервереБезКонтекста
Функция НайтиДействиеЗадания(Знач ИдентификаторКонструктора)
	
	ДействиеСсылка = Справочники.ШаблоныКоманд.ПустаяСсылка();
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Т.Ссылка КАК Ссылка,
	|	0 КАК Порядок
	|ИЗ
	|	Справочник.ДействияЗаданий КАК Т
	|ГДЕ
	|	Т.ИдентификаторКонструктора = &ИдентификаторКонструктора
	|	И Т.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	Запрос.УстановитьПараметр("ИдентификаторКонструктора",ИдентификаторКонструктора);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ДействиеСсылка = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ДействиеСсылка;
КонецФункции

#КонецОбласти

#Область ШаблонКоманды

&НаСервереБезКонтекста
Функция СоздатьШаблонКоманды(Знач ИдентификаторКонструктора, Знач Наименование, Знач ТекстШаблона)
	
	ШаблонСсылка = Справочники.ШаблоныКоманд.ПустаяСсылка();
	
	ШаблонОбъект = Справочники.ШаблоныКоманд.СоздатьЭлемент();
	ШаблонОбъект.Наименование = Наименование;
	ШаблонОбъект.ИдентификаторКонструктора 	= ИдентификаторКонструктора;
	ШаблонОбъект.ТекстШаблона 				= ТекстШаблона;
	
	Попытка
		ШаблонОбъект.Записать();
		ШаблонСсылка = ШаблонОбъект.Ссылка;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Сообщить(ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("СозатьШаблонКоманды",УровеньЖурналаРегистрации.Ошибка,Неопределено,Неопределено,ТекстОшибки);
	КонецПопытки;
	
	Возврат ШаблонСсылка;
КонецФункции

&НаСервереБезКонтекста
Функция НайтиШаблонКоманды(Знач ИдентификаторКонструктора,Знач ТекстШаблона="")
	
	ШаблонСсылка = Справочники.ШаблоныКоманд.ПустаяСсылка();
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Т.Ссылка КАК Ссылка,
	|	0 КАК Порядок
	|ИЗ
	|	Справочник.ШаблоныКоманд КАК Т
	|ГДЕ
	|	Т.ИдентификаторКонструктора = &ИдентификаторКонструктора
	|	И Т.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Ссылка,
	|	1
	|ИЗ
	|	Справочник.ШаблоныКоманд КАК Т
	|ГДЕ
	|	Т.ПометкаУдаления = ЛОЖЬ
	|	И (ВЫРАЗИТЬ(Т.ТекстШаблона КАК СТРОКА(1000))) = (ВЫРАЗИТЬ(&ТекстШаблона КАК СТРОКА(1000)))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	Запрос.УстановитьПараметр("ИдентификаторКонструктора",ИдентификаторКонструктора);
	Запрос.УстановитьПараметр("ТекстШаблона",ТекстШаблона);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ШаблонСсылка = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ШаблонСсылка;
КонецФункции
#КонецОбласти